declare namespace dc="http://purl.org/dc/elements/1.1/";
declare namespace rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#";
declare namespace rdfs="http://www.w3.org/2000/01/rdf-schema#";
declare namespace xsd="http://www.w3.org/2001/XMLSchema#";
declare namespace owl="http://www.w3.org/2002/07/owl#";
declare namespace time="http://www.w3.org/2006/time#"; 
declare namespace gr="http://purl.org/goodrelations/v1#";
declare namespace foaf="http://xmlns.com/foaf/0.1/";

declare namespace nub="http://resources.uitburo.nl/";
declare namespace ah = "http://purl.org/artsholland/1.0/";

import module namespace waag = "http://waag.org" at "http://localhost:8080/static/xquery/waag.xquery.jsp";

let $events := //event

let $baseuri := {"http://data.artsholland.com/"}
let $baseuriUitburo := {"http://resources.uitburo.nl/"}

return
	for $event in $events	
	
		let $cidn := {$event/cidn}
		let $euri := {concat($baseuri, "events/", $cidn)}
		let $euriUitburo := {concat($baseuriUitburo, "events/", $cidn)}
			
		let $puri := {concat($baseuri, "productions/", $event/link[@type="production"]/@cidn)}
		let $vuri := {concat($baseuri, "venues/", $event/link[@type="location"]/@cidn)}
				
		let $typeKey := {$event/type/@key}
		let $typeClass := {if (waag:empty($typeKey)) then () else concat("ah:eventType", waag:capitalize-first-only(data($typeKey)))}
		
		let $statusKey := {$event/status/@key}
		let $statusClass := {if (waag:empty($statusKey)) then () else concat("ah:eventStatus", waag:capitalize-first-only(data($statusKey)))}
			
		let $room := {$event/room}
		let $ruri := {if ($room eq "") then () else concat($vuri, "/rooms/", waag:make-uri($room))}
				
		let $title := {waag:replace-characters($event/title)}
		let $description := {waag:replace-characters($event/description)}
		
		construct { 
		
			$euri a ah:Event.
			$euri owl:sameAs $euriUitburo.
			
			$euri ah:cidn {$cidn}^^xsd:string.
			
			$vuri a ah:Venue.			
			$euri ah:venue $vuri.
			
			$puri a ah:Production.
			$euri ah:production $puri.
			
			$euri dc:created {$event/@date-created}^^xsd:string.
			$euri dc:modified {$event/@date-changed}^^xsd:string.
			
			$euri dc:title {if ($title eq "") then () else $title}^^xsd:string.
			$euri dc:description {if ($description eq "") then () else $description}^^xsd:string.
						
			$euri ah:eventType $typeClass.
			$typeClass a ah:EventType.			
			$typeClass rdfs:label {if (waag:empty($typeKey)) then () else $typeKey}^^xsd:string.
			
			$euri ah:eventStatus $statusClass.
			$statusClass a ah:EventStatus.			
			$statusClass rdfs:label {if (waag:empty($statusKey)) then () else $statusKey}^^xsd:string.
			
			{ for $pricetag in $event/pricetags//pricetag
	
				# Als er events zijn met pricetags met zelfde type: gebruik $pos in uri
				let $turi := {concat($euri, "/tickets/", waag:make-uri($pricetag/type))}
				let $puri := {concat($turi, "/price")}
				
				let $name := {waag:replace-characters($pricetag/type)}
				let $description := {waag:replace-characters($pricetag/description)}			
				
				construct {
				
					$euri ah:ticket $turi.
					$turi a gr:Offering.			
					
					$turi gr:name	{if ($name eq "") then () else $name}^^xsd:string.
					$turi dc:description {if ($description eq "") then () else $description}^^xsd:string.					
						
					# TODO:
					#	<ticketsalesinformation>kvk info</ticketsalesinformation>
					#	<ticketsalesphone>kvk telefoon</ticketsalesphone>
					#	<ticketsalesurl>kvk url</ticketsalesurl>
			
					$turi gr:hasPriceSpecification $puri.
					$puri a gr:UnitPriceSpecification.
			
					$puri gr:hasMinCurrencyValue {$pricetag/lowest}^^xsd:decimal.			
					$puri gr:hasMaxCurrencyValue {$pricetag/highest}^^xsd:decimal.
					$puri gr:hasCurrency "EUR"^^xsd:string.
								
				}
			}.
			
			# $pos is counter (http://stackoverflow.com/questions/5652397/xquery-counters-inside-a-for)
			{ for $medium at $pos in $event/media//medium				
				
				let $muri := {concat($euri, "/attachments/", $pos)}	
				let $description := {waag:replace-characters($medium/description)}
				let $title := {waag:replace-characters($medium/title)}
							
				let $typeKey := {waag:replace-characters($medium/type)}
				let $typeClass := {if ($typeKey eq "") then () else concat("ah:attachmentType", waag:capitalize-first-only(data($typeKey)))}
				
				construct {
				
				 	$euri ah:attachment $muri.
				 	$muri a ah:Attachment.
					$muri dc:title {if ($title eq "") then () else $title}^^xsd:string.
					$muri dc:description {if ($description eq "") then () else $description}^^xsd:string.
					
					# TODO: string?
					$muri ah:url {$medium/ref}^^xsd:string.
	
					# TODO:
					#	<alt>A family again</alt>
					
					$muri ah:attachmentType $typeClass.				
					$typeClass a ah:AttachmentType.			
					$typeClass rdfs:label {if ($typeKey eq "") then () else $typeKey}^^xsd:string.				
					
				}
			}.
			
			$ruri a ah:Room.
			$ruri rdfs:label {if ($room eq "") then () else $room}.
			$euri ah:room $ruri.
			$vuri ah:room $ruri.
			
			$euri time:hasBeginning {$event/datetime-start}^^xsd:dateTime.
			$euri time:hasEnd {$event/datetime-end}^^xsd:dateTime.
				 	
		}